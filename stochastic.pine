//@version=5
// Copyright (c) 2021-present, Alex Orekhov (everget)
indicator('Stochastic V5', shorttitle='StochV5', precision=2)

print(string txt) => 
    var table t = table.new(position.middle_right, 1, 1) 
    table.cell(t, 0, 0, txt, bgcolor = color.yellow)

var color NONE_COLOR = color.new(color.white, 100)

FAST_PERIOD_K = input.int(title='Fast Period', minval=1, defval=24)
SLOW_PERIOD_D = input.int(title='Slow Period', minval=1, defval=3)
SMOOTH_FAST_PERIOD = input.int(title='Smooth', minval=1, defval=5)
SRC = input(title='Source', defval=close)

OVERBOUGHT_LEVEL = input(title='Overbought Level', defval=70)
OVERSOLD_LEVEL = input(title='Oversold Level', defval=20)

MAX_LEVEL = input(title='Max Level', defval=100)
MIN_LEVEL = input(title='Min Level', defval=0)

fastPeriod = ta.sma(ta.stoch(SRC, high, low, FAST_PERIOD_K), SMOOTH_FAST_PERIOD)
slowPeriod = ta.sma(fastPeriod, SLOW_PERIOD_D)

trendColor = fastPeriod > slowPeriod ? #0ebb23 : color.red
fastPeriodPlot = plot(fastPeriod, title='%Fast', color=trendColor)
slowPeriodPlot = plot(slowPeriod, title='%Slow', color=trendColor)

histogram = fastPeriod - slowPeriod
histogramColor = histogram >= 0 ? histogram[1] < histogram ? #26A69A : #B2DFDB : histogram[1] < histogram ? #FFCDD2 : #EF5350
plot(histogram, title='Histogram', style=plot.style_columns, color=histogramColor)

overboughtLevelPlot = hline(OVERBOUGHT_LEVEL, title='Overbought Level', linestyle=hline.style_dotted, color=#00796b)
maxLevelPlot = hline(MAX_LEVEL, title='Max Level', linestyle=hline.style_dotted, color=NONE_COLOR)
hline(50, title='Middle Level', linestyle=hline.style_dotted, color=#989898)

oversoldLevelPlot = hline(OVERSOLD_LEVEL, title='Oversold Level', linestyle=hline.style_dotted, color=#f57f17)
minLevelPlot = hline(MIN_LEVEL, title='Min Level', linestyle=hline.style_dotted, color=NONE_COLOR)
fill(overboughtLevelPlot, oversoldLevelPlot, title='Middle Zone', color=color.new(color.purple, 95))

overboughtBreakoutColor = fastPeriod > OVERBOUGHT_LEVEL ? color.red : na
oversoldBreakoutColor = fastPeriod < OVERSOLD_LEVEL ? color.green : na
fill(maxLevelPlot, overboughtLevelPlot, title='Upper Band Breakout', color=color.new(overboughtBreakoutColor, 85))
fill(minLevelPlot, oversoldLevelPlot, title='Lower Band Breakout', color=color.new(oversoldBreakoutColor, 85))

fill(fastPeriodPlot, slowPeriodPlot, title='Ribbon', color=color.new(trendColor, 70))

middleCrossoverBgColor = fastPeriod > 50 ? color.green : color.red
bgcolor(color.new(middleCrossoverBgColor, 90))

plotshape(ta.crossover(fastPeriod, slowPeriod) ? fastPeriod : na, title='Fast/Slow Crossover', location=location.absolute, style=shape.circle, size=size.tiny, color=color.green)
plotshape(ta.crossunder(fastPeriod, slowPeriod) ? slowPeriod : na, title='Fast/Slow Crossunder', location=location.absolute, style=shape.circle, size=size.tiny, color=color.red)